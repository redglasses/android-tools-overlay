# Copyright 2020 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

inherit flag-o-matic toolchain-funcs

MY_PV="${PV/_p/_r}"
MY_P=${PN}-${MY_PV}

DESCRIPTION="tool for converting the HPROF file generated by Android SDK to a standard format"
HOMEPAGE="https://android.googlesource.com/platform/dalvik/"

SRC_URI="https://android.googlesource.com/platform/build/+archive/refs/tags/android-${MY_PV}/tools/zipalign.tar.gz -> ${P}.tar.gz
	https://android.googlesource.com/platform/external/zopfli/+archive/refs/tags/android-${MY_PV}/src/zopfli.tar.gz -> ${P}-zopfli.tar.gz"

LICENSE="Apache-2.0"
SLOT="0"
KEYWORDS="~amd64 ~arm ~arm64 ~x86"
IUSE=""

DEPEND="app-arch/zopfli:=
	dev-libs/android-core:=
	sys-libs/zlib:="
RDEPEND="${DEPEND}"
BDEPEND=""

S=${WORKDIR}

unpack_into() {
	local archive="$1"
	local dir="$2"

	mkdir -p "${dir}"
	pushd "${dir}" >/dev/null || die
	unpack "${archive}"
	popd >/dev/null
}

src_unpack() {
	unpack_into "${P}.tar.gz" .
	unpack_into "${P}-zopfli.tar.gz" zopfli
}

src_compile() {
	local x
	for x in ZipAlign ZipEntry ZipFile
	do
		$(tc-getCXX) ${CFLAGS} ${CPPFLAGS} \
			-I/usr/include \
			-c -o ${x}.o ${x}.cpp || die "compile ${PN} failed"
	done
	$(tc-getCXX) \
		${LDFLAGS} -o ${PN} ZipAlign.o ZipEntry.o ZipFile.o -L/usr/lib \
		-lbase -lcutils -llog -lpthread -lutils -lz -lziparchive -lzopfli || die "link ${PN} failed"
}

src_install() {
	dobin ${PN}
}
